buildscript {
  ext {
    kotlinVersion = '1.1.61'
    springBootVersion = '1.5.9.RELEASE'
  }
  repositories {
    jcenter()
    mavenCentral()
  }
  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
    classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    classpath "org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}"
    classpath "org.jetbrains.kotlin:kotlin-noarg:${kotlinVersion}"
    classpath 'com.eriwen:gradle-css-plugin:2.12.0'
    classpath 'com.eriwen:gradle-js-plugin:2.12.0'
  }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'org.springframework.boot'
apply plugin: 'css'
apply plugin: 'js'

apply plugin: "kotlin-allopen"
apply plugin: "kotlin-spring"
apply plugin: "kotlin-noarg"

jar {
  baseName = 'sage'
  version = '1.0-SNAPSHOT'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
compileJava.options.compilerArgs.add '-parameters'
compileTestJava.options.compilerArgs.add '-parameters'
compileKotlin.kotlinOptions.jvmTarget = '1.8'
compileTestKotlin.kotlinOptions.jvmTarget = '1.8'

springBoot {
  mainClass = 'sage.ApplicationKt'
}

noArg {
  annotation("sage.annotation.KotlinNoArg")
}

repositories {
  mavenCentral()
}

dependencies {
  compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:${kotlinVersion}"
  compile "org.springframework.boot:spring-boot-starter-aop:${springBootVersion}"
  compile "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"

  compile 'com.github.httl:httl:1.0.11'
  compile 'com.github.httl:httl-springmvc:1.0.11'
  compile ('org.avaje.ebeanorm:avaje-ebeanorm:7.9.1') {
    exclude group: 'org.slf4j', module: 'slf4j-api'
    exclude group: 'ch.qos.logback', module: 'logback-classic'
  }
  compile 'org.avaje.ebeanorm:avaje-ebeanorm-agent:4.9.2'
  compile 'org.avaje:avaje-agentloader:2.1.2'

  compile 'org.jasypt:jasypt:1.9.2'
  compile 'org.apache.commons:commons-lang3:3.4'
  compile 'commons-fileupload:commons-fileupload:1.3.1'
  compile 'org.elasticsearch:elasticsearch:2.4.0'
  compile 'org.jsoup:jsoup:1.10.2'
  compile 'com.atlassian.commonmark:commonmark:0.9.0'
  compile 'com.atlassian.commonmark:commonmark-ext-autolink:0.9.0'
  compile 'com.atlassian.commonmark:commonmark-ext-gfm-strikethrough:0.9.0'
  compile 'com.atlassian.commonmark:commonmark-ext-gfm-tables:0.9.0'
  compile 'com.atlassian.commonmark:commonmark-ext-heading-anchor:0.9.0'

  runtime 'mysql:mysql-connector-java:5.1.38'

  testCompile "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
}

ext {
  staticSrc = 'src/main/webapp/static'
}

css.source {
  dev {
    css {
      srcDir "${staticSrc}/css"
      include "*.css"
    }
  }
}

combineCss {
  source = css.source.dev.css.files
  dest = "${staticSrc}/css-bin/all.css"
}

minifyCss {
  source = combineCss
  dest = "${staticSrc}/css-bin/all.css"
}

javascript.source {
  dev {
    js {
      srcDir "src/main/webapp/static/js"
      include "*.js"
    }
  }
}

task combinejs(type: com.eriwen.gradle.js.tasks.CombineJsTask) {
  source = javascript.source.dev.js.files
  dest = file("${staticSrc}/js-bin/all.js")
}

task minifyjs(type: com.eriwen.gradle.js.tasks.MinifyJsTask) {
  source = combinejs
  dest = file("${staticSrc}/js-bin/all.js")
  closure {
    warningLevel = 'QUIET'
  }
}

task web {
  dependsOn 'combineCss'
  dependsOn 'combinejs'
}

task webmin {
  dependsOn 'minifyCss'
  dependsOn 'minifyjs'
}